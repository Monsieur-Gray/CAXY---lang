ws = _{" "*}
newln = _{("\r\n" | "\n") ~ ws}
special_chars = _{"*"| "?"| "#"| "@" }
extra_special_chars = _{"-" | "+" | "*" | "/" | "=" | "_" | ":" |  "(" | ")"}
char = _{ASCII_ALPHANUMERIC | special_chars }
decimal = _{"."}

INT = @{("-" | "+"){0,1} ~ ASCII_DIGIT+}
FLOAT = @{ASCII_DIGIT+ ~ws~ decimal ~ws~ ASCII_DIGIT+}
STRLIT = @{( "\'" ~ws~   (char | " " | special_chars | extra_special_chars )*   ~ws~ "\'" )}
ID = @{"?"{0,1} ~ws~ char+}
BOOL = @{("true" | "false")}
REGISTER = @{"$" ~ ASCII_DIGIT}
D_TYPE = _{ BOOL | REGISTER | FLOAT | INT | STRLIT | ID  }
ErrLine = {(char | " " | "[" | "]")+}


Std_fn = {"PRNT_COOL" |"PRNT" | "SINPUT" }
Operations = { "ADD" | "SUB" | "MUL" | "DIV" }
MemType = { "int" | "float" | "str" | "bool" }
MemInst = {"MOV" | "DEL"}
Comment = {"crap:-" ~ws~ (char | " "+)*}
conditional_oper = {"==" | "!=" | ">" | "<" }
logical_oper = {"&&" | "||" }


var_make = { ws ~ MemType ~ws~ ID ~ws~ (D_TYPE) ~ws}
vsec = {"_VARS:" ~ws~ INT ~newln* ~ (var_make ~ newln*)* ~ws~ "EOS!"}


stdfn_expr = {Std_fn ~ (ws~ (math_expr | logical_expr | D_TYPE ) )+ ~ws}
math_expr = {Operations ~ (ws~ D_TYPE)+ ~ws}
mem_inst_expr = {MemInst ~ws~ (ID | REGISTER) ~ws~ (math_expr | stdfn_expr | D_TYPE) ~ws}

condition = {D_TYPE ~ws~ conditional_oper ~ws~ D_TYPE }
logical_expr = {"[" ~ws~ (condition | logical_expr) ~ws~ (ws~ logical_oper ~ws~ (condition | logical_expr)){0,1} ~ws~ "]" ~ws}

jump_expr = {"JUMPIF" ~ws~ INT ~ws~ (BOOL | logical_expr) ~ws}
if_expr = {"IF" ~ws~ (BOOL | ID | logical_expr) ~ws~ parent_expr ~ws}

parent_expr = _{stdfn_expr | mem_inst_expr | math_expr | jump_expr | logical_expr  | if_expr | Comment | ErrLine}

msec = {"_MAIN:" ~newln*~ (ws~ parent_expr ~ws~ newln*)* ~ "_END:" ~ws~ newln*}

file = { SOI ~ vsec ~newln*~ msec  ~ws~ EOI }